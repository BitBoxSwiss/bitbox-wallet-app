apply plugin: 'com.android.application'

def repoRoot = project.projectDir.parentFile.parentFile.parentFile.parentFile
def appVersionFile = new File(repoRoot, "APP_VERSION")
if (!appVersionFile.exists()) {
    throw new GradleException("APP_VERSION not found at ${appVersionFile}")
}
def appVersion = appVersionFile.text.trim()

android {
    compileSdk 35
    // Keep in sync with android-env.mk.inc and the NDK installed in docker_install.sh.
    ndkVersion "28.2.13676358"
    defaultConfig {
        applicationId "ch.shiftcrypto.bitboxapp"
        minSdk 24
        // minSdkVersion should match the `-androidapi` gomobile bind flag
        // in backend/mobileserver/Makefile
        targetSdkVersion 35
        versionCode 68
        versionName "${appVersion}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++11'
            }
        }
    }
    signingConfigs {
        release {
            def env = System.getenv()
            if (env.containsKey('KEYSTORE_STORE_FILE')) {
                keyAlias env['KEYSTORE_KEY_ALIAS']
                keyPassword env['KEYSTORE_KEY_PASSWORD']
                storeFile file(env['KEYSTORE_STORE_FILE'])
                storePassword env['KEYSTORE_STORE_PASSWORD']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "BitBoxApp"
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "BitBoxApp DEBUG"
        }
    }
    namespace 'ch.shiftcrypto.bitboxapp'
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.31.6+'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.activity:activity:1.8.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
    implementation project(path: ':mobileserver')
    // The frontend depends on qr-scanner (https://github.com/nimiq/qr-scanner)
    // which uses the Shape Detection API BarcodeDetector.  We use Google Play Services
    // to interact with the native barcode scanner. See (2):
    // https://developers.google.com/ml-kit/vision/barcode-scanning/android
    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.0'


    // Fix Duplicate class
    // see (https://gist.github.com/danielcshn/7aa57155d766d46c043fde015f054d40)
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
}
